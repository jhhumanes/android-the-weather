plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def propertiesFile = rootProject.file("theweather.properties")
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.josehumaneshumanes.theweather"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "AEMET_APIURL", properties['AEMET_APIURL'])
        buildConfigField("String", "AEMET_APIKEY", properties['AEMET_APIKEY'])
        buildConfigField("String", "CITIES_RELOAD_EXPIRES_MINUTES", properties['CITIES_RELOAD_EXPIRES_MINUTES'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation kotlinLibs.values()
    implementation androidLibs.values()
    implementation androidKtxLibs.values()
    implementation hiltLibs.values()
    implementation retrofitLibs.values()
    implementation roomLibs.values()

    annotationProcessor annotationProcessorLibs.values()

    kapt androidKaptLibs.values()
    kaptAndroidTest androidTestKaptLibs.values()
    kaptTest testKaptLibs.values()

    testImplementation testLibs.values()
    androidTestImplementation androidTestLibs.values()
}

kapt {
    correctErrorTypes true
    javacOptions {
        // These options are normally set automatically via the Hilt Gradle plugin, but we
        // set them manually to workaround a bug in the Kotlin 1.5.20
        option("-Adagger.fastInit=ENABLED")
        option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
    }
}
